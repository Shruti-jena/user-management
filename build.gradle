plugins {
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'java'
    id 'jacoco'
}

group = 'com.shruti'
version = '0.0.1-SNAPSHOT'
java {
    sourceCompatibility = '21'
    targetCompatibility = '21'
}


repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'   // ✅ Needed for @Entity, JpaRepository
    implementation 'org.springframework.boot:spring-boot-starter-validation' // ✅ Needed for @NotBlank, @Email
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // JWT (JSON Web Token)
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5' // for JSON serialization

    // Lombok (optional but useful)
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // H2 Database (for testing)
    runtimeOnly 'com.h2database:h2'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    //Swagger-API
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

}

tasks.named('test') {
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test

    afterEvaluate {
        classDirectories.setFrom(
            files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                    '**/DTO/**',
                    '**/Config/**',
                    '**/Security/**',
                    '**/Exception/**',
                    '**/*Application.class'
                ])
            })
        )
    }

    reports {
        html.required = true
        xml.required = true
    }
}


jacocoTestCoverageVerification {

    violationRules {

        rule {
            element = 'CLASS'

            includes = [
                'com.shruti.user_management.Service.*',
                'com.shruti.user_management.Controller.*',
                'com.shruti.user_management.Model.*'
            ]

            excludes = [
                '**/DTO/**',
                '**/Security/**',
                '**/*Application*',
                '**/Exception/**'
            ]

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.50
            }
        }
    }
}


check.dependsOn jacocoTestCoverageVerification